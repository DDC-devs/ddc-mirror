
module Runtime.Info

export foreign c value
 ddcInfoInitStatic              : Unit    -> Unit
 ddcInfoFrameNew                : Word32# -> Addr#
 ddcInfoFramePush               : Addr#   -> Unit
 ddcInfoFrameAddData            : Addr#   -> Word16# -> Word16# -> Addr# -> Addr# -> Word32#

 ddcInfoFrameAddSuper
        :  Addr#   -> Word16# -> Word16# -> Addr# -> Addr#
        -> Word64# -> Word64# -> Word64# -> Word64#
        -> Word32#

 ddcInfoEntryLookup             : Word32# -> Addr#
 ddcInfoEntryGetIdent           : Addr# -> Word32#
 ddcInfoEntryGetType            : Addr# -> Word16#

 ddcInfoEntryGetDataTag         : Addr# -> Word16#
 ddcInfoEntryGetDataArity       : Addr# -> Word16#
 ddcInfoEntryGetDataModuleName  : Addr# -> Addr#
 ddcInfoEntryGetDataName        : Addr# -> Addr#

 ddcInfoEntryGetSuperParams     : Addr# -> Word16#
 ddcInfoEntryGetSuperBoxes      : Addr# -> Word16#
 ddcInfoEntryGetSuperModuleName : Addr# -> Addr#
 ddcInfoEntryGetSuperName       : Addr# -> Addr#


import foreign c value
 malloc                         : Nat# -> Addr#
 ddcPrimStdoutPutNat            : Nat#  -> Void#

with letrec

---------------------------------------------------------------------------------------------------
-- | Initialize the static portion of the info table.
--
--   This adds info table entries for types that are defined by the runtime
--   system. Info table entries for user defined types are added by the
--   module initialization code of the defining module.
--
--   The names for primitive types need to match the ones in the
--   DDC.Core.Discus.Convert.Data module of the compiler implementation.

--   The indices need to match the ones used in
--   System.Runtime.Reflect of the base library.
--
ddcInfoInitStatic (_: Unit): Unit
 = do
        -- Allocate the info table frame,
        --   which has one entry for the sential 0 index,
        --   and one for each of the entries we add in this function.
        aFrame  = ddcInfoFrameNew (add# 1w32# 14w32#)

        -- 1 Base.Unit
        iUnit   = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert#  "Unit"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Unit"#) 0# iUnit

        -- 2 Base.TextLit
        -- CAREFUL: ddcPrimMakeTextLit in Text.dcs uses the info table index 2 as a magic number.
        iTxLit  = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "TextLit"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.TextLit"#) 0# iTxLit

        -- 3 Base.Bool
        iBool   = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert#  "Bool"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Bool"#) 0# iBool

        -- 4 Base.Nat
        iNat    = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Nat"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Nat"#) 0# iNat

        -- 5 Base.Int
        -- CAREFUL: ddcPrimMakeInt in Numeric.dcs uses the info table index 5 as a magic number.
        iInt    = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Int"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Int"#) 0# iInt

        -- 6 Base.Size
        iSize   = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Size"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Size"#) 0# iSize

        -- 7 Base.Word8
        iWord8  = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Word8"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Word8"#) 0# iWord8

        -- 8 Base.Word16
        iWord16  = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Word16"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Word16"#) 0# iWord16

        -- 9 Base.Word32
        iWord32 = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Word32"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Word32"#) 0# iWord32

        -- 10 Base.Word64
        iWord64 = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Word64"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Word64"#) 0# iWord64

        -- 11 Base.Float32
        iFloat32 = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Float32"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Float32"#) 0# iFloat32

        -- 12 Base.Float64
        iFloat64 = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Float64"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Float64"#) 0# iFloat64

        -- 13 Base.Addr
        iAddr   = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Addr"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Addr"#) 0# iAddr

        -- 14 Base.Ptr
        iPtr    = ddcInfoFrameAddData aFrame 0w16# 0w16# (convert# "Base"#) (convert# "Ptr"#)
        write# [Word32#] (globali# [Word32#] "ddcInfoIndex.data.Base.Ptr"#) 0# iPtr

        ddcInfoFramePush aFrame


---------------------------------------------------------------------------------------------------
-- While the runtime system is being initialized the infotable consists
-- of a number of frames joined by a linked list. Each frame contains
-- entries that describe the data objects that can be created by the
-- running user program.
--
-- TODO:
-- During runtime initialization we call separate initialization code for
-- each module, which adds its information to the list of frames. As a
-- final step we then pack down all the frame into a single one, so that
-- entries can be retrieved in constant time, rather than by walking through
-- the linked list of frames.
--
-- struct InfoFrame
-- { 0  uint32       sizeUsed     -- Total number of used entries in the frame.
--   4  uint32       sizeMax      -- Maximum number of entries in the frame.
--   8  addr64       next         -- Address of next frame, or 0 if no more frames.
--   16 InfoEntry[]  entries      -- Entries
-- }
--
-- struct InfoEntry
-- {  uint32 ident
--    uint16 type
-- }
--
-- struct InfoEntryData <: InfoEntry
-- {                            -- Total size = 64 bytes.
--    0  uint32  ident          -- Identifier of entry in infotable.
--    4  uint16  type   = 0     -- Type of entry in infotable (0 = Data)
--    6  uint16  tag            -- Tag of data constructor in its data type.
--    8  uint16  arity          -- Arity of data constructor.
--    10 uint8[] .. padding ..
--    16 addr64  moduleName     -- Address of null-terminated string of module name.
--    24 addr64  ctorName       -- Address of null-terminated string of constructor name.
--    32 uint8[] .. padding ..  -- (so that all frames have the same length)
-- }
--
-- struct InfoEntrySuper <: InfoEntry
-- {                            -- Total size = 64 bytes.
--    0  uint32  ident          -- Identifier of entry in infotable.
--    4  uint16  type   = 1     -- Type of entry in infotable (1 = Super)
--    6  uint16  params         -- Number of parameters of function.
--    8  uint16  boxes          -- Number of inner boxes of function.
--    10 uint8[] .. padding ..
--    16 addr64  moduleName     -- Address of null-terminated string of module name.
--    24 addr64  ctorName       -- Address of null-terminated string of constructor name.
--    32 word64  hash0          -- Hash of super expression (word 0)
--    40 word64  hash1          -- ...
--    48 word64  hash2          -- ...
--    56 word64  hash3          -- ...
-- }


-- | Allocate a new info table frame with enough space for the
--   given number of entries.
ddcInfoFrameNew (nEntries: Word32#): Addr#
 = do
        nSizeOfEntry    = 32#

        nSizeOfFrame
         =      add# (size# [Word32#])                  -- Total number of entries.
               (add# (size# [Word32#])                  -- Max number of entries.
               (add# (size# [Addr#])                    -- Address of next frame.
                     (mul#  (promote# nEntries) nSizeOfEntry)))

        aFrame          = malloc nSizeOfFrame
        set# aFrame 0w8# nSizeOfFrame

        write# [Word32#] aFrame 0# 0w32#                -- Used entries in frame.
        write# [Word32#] aFrame 4# nEntries             -- Total entries in frame.
        write# [Addr#]   aFrame 8# (promote# 0w64#)     -- Address of next frame.

        aFrame


-- | Push an info table frame onto the global frame stack.
ddcInfoFramePush (aFrame: Addr#): Unit
 = do
        aRoot   = read# (global# [Addr#] "ddcInfoFrameRoot"#) 0#
        write# [Addr#] aFrame 8# aRoot
        write# [Addr#]  (global# [Addr#] "ddcInfoFrameRoot"#) 0# aFrame
        ()


-- | Add an entry for a data constructor to the given info frame,
--   using the next available table index from the global index counter.
ddcInfoFrameAddData
        (aFrame:        Addr#)          -- Address of frame to add info entry to.
        (nTag:          Word16#)        -- Tag of data constructor in its data type.
        (nArity:        Word16#)        -- Arity of data constructor.
        (aNameModule:   Addr#)          -- Address of null-terminated string of module name.
        (aNameCtor:     Addr#)          -- Address of null-terminated string of constructor name.
        : Word32#                       -- Index of new entry.
 = do
        nSizeOfEntry    = 32#

        -- Check that we're not about to overflow the frame.
        nFrameUsed      = read# [Word32#] aFrame 0#
        nFrameMax       = read# [Word32#] aFrame 4#
        (case ge# nFrameUsed nFrameMax of
          True#  -> fail#
          False# -> ())

        -- Allocate a global identifier for the new entry.
        nIdent  = read# (global# [Word32#] "ddcInfoIdentNext"#) 0#
        write# (global# [Word32#] "ddcInfoIdentNext"#) 0# (add# nIdent 1w32#)

        -- Initialize the entry in the frame.
        aEntry  = plusAddr# aFrame
                 (add#  16#
                        (mul# (promote# nFrameUsed) 64#))

        write# [Word32#] aEntry 0#  nIdent      -- Entry identifier.
        write# [Word16#] aEntry 4#  0w16#       -- Entry type (0 = Data)
        write# [Word16#] aEntry 6#  nTag        -- Tag of data constructor.
        write# [Word16#] aEntry 8#  nArity      -- Arity of data constructor.
        write# [Addr#]   aEntry 16# aNameModule -- Module name.
        write# [Addr#]   aEntry 24# aNameCtor   -- Constructor name.

        -- Bump the count of entries in the frame.
        write# [Word32#] aFrame 0#  (add# nFrameUsed 1w32#)

        nIdent


-- | Add an entry for a top level supercombinator to the given info frame,
--   using the next available table index from the global index counter.
--
--   We call this a 'Super(combinator)' to distinguish it from nested functions
--   that don't have names.
--
--   This code is currently identical to the ddcInfoFrameAddData, but we'll
--   add the hash of the super ast in a subsequent step.
--
ddcInfoFrameAddSuper
        (aFrame:        Addr#)          -- Address of frame to add info entry to.
        (nParams:       Word16#)        -- Number of value parameters for super.
        (nBoxes:        Word16#)        -- Number of inner boxes of super.
        (aNameModule:   Addr#)          -- Address of null-terminated string of module name.
        (aNameSuper:    Addr#)          -- Address of null-terminated string of super name.
        (wHash0:        Word64#)        -- Hash of super expression (word0)
        (wHash1:        Word64#)        -- ...
        (wHash2:        Word64#)        -- ...
        (wHash3:        Word64#)        -- ...
        : Word32#                       -- Index of new entry.
 = do
        nSizeOfEntry    = 64#

        -- Check that we're not about to overflow the frame.
        nFrameUsed      = read# [Word32#] aFrame 0#
        nFrameMax       = read# [Word32#] aFrame 4#
        (case ge# nFrameUsed nFrameMax of
          True#  -> fail#
          False# -> ())

        -- Allocate a global identifier for the new entry.
        nIdent  = read# (global# [Word32#] "ddcInfoIdentNext"#) 0#
        write# (global# [Word32#] "ddcInfoIdentNext"#) 0# (add# nIdent 1w32#)

        -- Initialize the entry in the frame.
        aEntry  = plusAddr# aFrame
                 (add#  16#
                        (mul# (promote# nFrameUsed) 64#))

        write# [Word32#] aEntry 0#  nIdent      -- Entry identifier.
        write# [Word16#] aEntry 4#  1w16#       -- Entry type (0 = Super)
        write# [Word16#] aEntry 6#  nParams     -- Tag of data constructor.
        write# [Word16#] aEntry 8#  nBoxes      -- Arity of data constructor.
        write# [Addr#]   aEntry 16# aNameModule -- Module name.
        write# [Addr#]   aEntry 24# aNameSuper  -- Super name.
        write# [Word64#] aEntry 32# wHash0      -- Hash of super expression (word0)
        write# [Word64#] aEntry 40# wHash1      -- ..
        write# [Word64#] aEntry 48# wHash2      -- ..
        write# [Word64#] aEntry 56# wHash3      -- ..

        -- Bump the count of entries in the frame.
        write# [Word32#] aFrame 0#  (add# nFrameUsed 1w32#)

        nIdent


-- Entry ------------------------------------------------------------------------------------------
ddcInfoEntryGetIdent      (aEntry: Addr#): Word32#
 =      read# [Word32#] aEntry 0#

ddcInfoEntryGetType       (aEntry: Addr#): Word16#
 =      read# [Word16#] aEntry 4#


-- Data
ddcInfoEntryGetDataTag   (aEntry: Addr#): Word16#
 =      read# [Word16#] aEntry 6#

ddcInfoEntryGetDataArity (aEntry: Addr#): Word16#
 =      read# [Word16#] aEntry 8#

ddcInfoEntryGetDataModuleName (aEntry: Addr#): Addr#
 =      read# [Addr#]   aEntry 16#

ddcInfoEntryGetDataName  (aEntry: Addr#): Addr#
 =      read# [Addr#]   aEntry 24#


-- Super
ddcInfoEntryGetSuperParams (aEntry: Addr#): Word16#
 =      read# [Word16#] aEntry 6#

ddcInfoEntryGetSuperBoxes  (aEntry: Addr#): Word16#
 =      read# [Word16#] aEntry 8#

ddcInfoEntryGetSuperModuleName (aEntry: Addr#): Addr#
 =      read# [Addr#]   aEntry 16#

ddcInfoEntryGetSuperName  (aEntry: Addr#): Addr#
 =      read# [Addr#]   aEntry 24#


---------------------------------------------------------------------------------------------------
-- | Lookup the info table entry with the given identifier,
--   failing if it cannot be found.
ddcInfoEntryLookup
        (nIdent:        Word32#)        -- Identifier of entry to lookup.
        : Addr#                         -- Address of info table entry.
 = do
        -- Get the addres of the first frame from the global root pointer.
        aFrame  = read# (global# [Addr#] "ddcInfoFrameRoot"#) 0#

        -- Try to find the entry in the list of frames.
        ddcInfoEntryLookup_loop nIdent aFrame 0w32#

ddcInfoEntryLookup_loop
        (nIdent:        Word32#)
        (aFrame:        Addr#)
        (nEntry:        Word32#)
        : Addr#
 = do
        -- Check the frame pointer is not null.
        (case eq# aFrame (promote# 0#) of
          True#  -> fail#
          False# -> ())

        -- If we're off the end of the current frame then go to the next one.
        nFrameUsed      = read# [Word32#] aFrame 0#
        nFrameMax       = read# [Word32#] aFrame 4#
        case or# (ge# nEntry nFrameUsed) (ge# nEntry nFrameMax) of
         True#  -> do
          aFrameNext   = read# [Addr#] aFrame 8#
          ddcInfoEntryLookup_loop nIdent aFrameNext 0w32#

         False# -> do
                -- Get the address of the current entry in the frame.
                aEntry  = plusAddr# aFrame
                         (add#  16#
                               (mul# (promote# nEntry) 32#))

                -- See if this is the entry we were looking for.
                nIdentx = read# [Word32#] aEntry 0#
                case eq# nIdent nIdentx of
                 True#  -> aEntry
                 False# -> ddcInfoEntryLookup_loop nIdent aFrame (add# nEntry 1w32#)


