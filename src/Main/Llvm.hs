{-# OPTIONS -fwarn-unused-imports #-}

-- | Wrappers for compiler stages dealing with LLVM code.
module Main.Llvm
	(compileViaLlvm)
where

-- main stages
import Main.Setup
import Main.Sea
import Main.Util

import DDC.Main.Error
import DDC.Main.Pretty
import DDC.Var

import qualified Module.Scrape		as M
import qualified DDC.Main.Arg		as Arg
import qualified DDC.Config.Version	as Version

import Llvm
import Llvm.Invoke
import Llvm.GhcReplace.Unique

import Sea.Exp
import Sea.Util				(eraseAnnotsTree)
import Sea.Pretty

import Util
import qualified Data.Map		as Map
import qualified Config.Config		as Config

import qualified Debug.Trace		as Debug

stage = "Main.Llvm"

debug = False

trace s v
 =	if debug
	  then Debug.trace s v
	  else v


compileViaLlvm
	:: (?verbose :: Bool, ?pathSourceBase :: FilePath)
	=> Setup			-- ^ Compile setup.
	-> ModuleId			-- ^ Module to compile, must also be in the scrape graph.
	-> Tree ()			-- ^ The Tree for the module.
	-> FilePath			-- ^ FilePath of source file.
	-> [FilePath]			-- ^ C import directories.
	-> [FilePath]			-- ^ C include files.
	-> Map ModuleId [a]		-- ^ Module import map.
	-> Bool				-- ^ Module defines 'main' function.
	-> M.Scrape			-- ^ ScrapeGraph of this Module.
	-> Map ModuleId M.Scrape	-- ^ Scrape graph of all modules reachable from the root.
	-> Bool				-- ^ Whether to treat a 'main' function defined by this module
					--	as the program entry point.
	-> IO Bool

compileViaLlvm
	setup modName eTree pathSource importDirs includeFilesHere importsExp
	modDefinesMainFn sRoot scrapes_noRoot blessMain
 = do
	let ?args		= setupArgs setup

	outVerb $ ppr $ "  * Write C header\n"
	writeFile (?pathSourceBase ++ ".ddc.h")
		$ makeSeaHeader
			eTree
			pathSource
			(map ((\(Just f) -> f) . M.scrapePathHeader)
					$ Map.elems scrapes_noRoot)
			includeFilesHere

	outVerb $ ppr $ "  * Generating LLVM IR code\n"
	llvmSource	<- outLlvm modName eTree pathSource

	writeFile (?pathSourceBase ++ ".ddc.ll")
			$ ppLlvmModule llvmSource

	invokeLlvmCompiler ?pathSourceBase []
	invokeLlvmAssembler ?pathSourceBase []

	return modDefinesMainFn


-- | Create LLVM source files
outLlvm
	:: (?args :: [Arg.Arg])
	=> ModuleId
	-> (Tree ())		-- sea source
	-> FilePath		-- path of the source file
	-> IO LlvmModule

outLlvm moduleName eTree pathThis
 = do
	-- Break up the sea into parts.
	let 	([ 	seaProtos, 		seaSupers
		 , 	seaCafProtos,		seaCafSlots,		seaCafInits
		 ,	seaData
		 , 	seaHashDefs ], junk)

		 = partitionFs
			[ (=@=) PProto{}, 	(=@=) PSuper{}
			, (=@=) PCafProto{},	(=@=) PCafSlot{},	(=@=) PCafInit{}
			, (=@=) PData{}
			, (=@=) PHashDef{} ]
			eTree

	when (not $ null junk)
	 $ panic "Main.Llvm" $ "junk sea bits = " ++ show junk ++ "\n"

	-- Build the LLVM code
	let comments =	[ "---------------------------------------------------------------"
			, "      source: " ++ pathThis
			, "generated by: " ++ Version.ddcName
			, "" ]

	let aliases = [ ("struct.Obj", ddcObj) ]

	let code = seaCafInits ++ seaSupers

	let fwddecls	= [_panicOutOfSlots, _panicSlotUnderflow]

	let globals	= moduleGlobals
			++ (catMap seaToLlvmGlobal $ eraseAnnotsTree seaCafSlots)

	decls		<- catMapM seaToLlvmDecls $ eraseAnnotsTree code

	return $ LlvmModule comments aliases globals fwddecls decls


seaToLlvmDecls :: Top (Maybe a) -> IO [LlvmFunction]
seaToLlvmDecls (PCafInit v t ss)
 = panic stage "Implement  'seaToLlvmDecls (PCafInit v t ss)'"


seaToLlvmDecls (PSuper v p t ss)
 = do	when debug
	  $ putStrLn "--------------------------------------------------------"
	{-
	putStrLn $ seaVar False v
	putStrLn ""
	mapM_ (\s -> putStrLn $ show s) ss
	putStrLn "--------------------------------------------------------"
	-}
	blocks <- catMapM seaToLlvm ss
 	return $ [ LlvmFunction
		(LlvmFunctionDecl (seaVar False v) External CC_Ccc (toLlvmType t) FixedArgs (map toParams p) Nothing)
		(map (seaVar True . fst) p)	-- funcArgs
		[]				-- funcAttrs
		Nothing				-- funcSect
		[ LlvmBlock (fakeUnique "entry") (blocks ++ [ Return Nothing ]) ]	-- funcBody
		]


toParams :: (Var, Type) -> LlvmParameter
toParams (v, t) = (toLlvmType t, [])


seaToLlvmGlobal :: Top (Maybe a) -> [LMGlobal]
seaToLlvmGlobal (PCafSlot v t)
 | t == TObj	-- TODO: This should be 'TPtr (TPtr TObj)'. Fix it upstream.
 =	let	tt = toLlvmType (TPtr (TPtr TObj))
		var = LMGlobalVar
 			("_ddcCAF_" ++ seaVar False v)	-- Variable name
			tt				-- LlvmType
			Internal			-- LlvmLinkageType
			Nothing				-- LMSection
			ptrAlign			-- LMAlign
			False				-- LMConst
	in [(var, Just (LMStaticLit (LMNullLit tt)))]

 | otherwise
 = panic stage $ "seaToLlvmGlobal on type : " ++ show t

moduleGlobals :: [LMGlobal]
moduleGlobals
 = 	[ ( ddcSlotPtr, Nothing )
	, ( ddcSlotMax, Nothing )
	, ( ddcSlotBase, Nothing ) ]


seaToLlvm :: Stmt a -> IO [LlvmStatement]

seaToLlvm (SBlank)	= return $ [Comment [""]]
seaToLlvm (SEnter n)	= return $ runtimeEnter n
seaToLlvm (SLeave n)	= return $ runtimeLeave n
seaToLlvm (SComment s)	= return $ [Comment [s]]

seaToLlvm (SGoto var)
 =	return $ [Branch (LMNLocalVar (seaVar False var) LMLabel)]

seaToLlvm (SAuto v t)
 =	-- LLVM is SSA so auto variables do not need to be declared.
	return [Comment ["SAuto " ++ seaVar True v ++ " " ++ show t]]

seaToLlvm (SLabel l)
 =	-- LLVM does not allow implicit fall through to a label, so
	-- explicitly branch to the label immediately following.
	let label = fakeUnique (seaVar False l)
	in return $ [Branch (LMLocalVar label LMLabel), MkLabel label]

seaToLlvm (SHackery s)
 =	-- Left over inline C code.
	panic stage $ "SHakery '" ++ s ++ "' not supported in LLVM backend."



seaToLlvm (SAssign (XVar v1 t1) t (XVar v2 t2))
 | t1 == TPtr (TPtr TObj) && t2 == TPtr (TPtr TObj) && t == TPtr (TPtr TObj)
	&& isGlobalVar v1 && isGlobalVar v2
 = do	tmp	<- newUniqueLocal (toLlvmType t1)
	return	$ [ Assignment tmp (loadAddress (toLlvmVar v2 t2))
		  , Store tmp (pVarLift (toLlvmVar v1 t1)) ]


seaToLlvm (SAssign x1 t x2)
 = do	when debug
	  $ putStrLn $ "SAssign " ++ (take 150 $ show x1)
	return []

seaToLlvm stmt
 = do	when debug
	  $ putStrLn $ take 150 $ show stmt
	return []





--------------------------------------------------------------------------------
-- Runtime hacks.

runtimeEnter :: Int -> [LlvmStatement]
runtimeEnter count
 =	[ Comment ["_ENTER (" ++ show count ++ ")"]
	, Assignment enter0 (Load ddcSlotPtr)
	, Assignment enter1 (GetElemPtr True enter0 [LlvmIndexInt count])
	, Store enter1 ddcSlotPtr

	, Assignment enter2 (Load ddcSlotMax)
	, Assignment enter3 (Compare LM_CMP_Ult enter1 enter2)
	, BranchIf enter3 (LMLocalVar egood LMLabel) (LMLocalVar epanic LMLabel)
	, MkLabel epanic
	, Expr (Call StdCall (LMGlobalVar "_panicOutOfSlots" (LMFunction _panicOutOfSlots) External Nothing Nothing True) [] [NoReturn])
	, Branch (LMLocalVar egood LMLabel)
	, MkLabel egood
	, Comment ["----- Slot initiialization -----"]
	]

	++ slotInit egood count
	++ [ Comment ["---------------------------------------------------------------"] ]
    where
	enter0 = LMNLocalVar "enter.0" ppObj
	enter1 = LMNLocalVar "enter.1" ppObj
	enter2 = LMNLocalVar "enter.2" ppObj
	enter3 = LMNLocalVar "enter.3" i1
	epanic = fakeUnique "enter.panic"
	egood = fakeUnique "enter.good"


runtimeLeave :: Int -> [LlvmStatement]
runtimeLeave count
 =	[ Comment ["---------------------------------------------------------------"]
	, Comment ["_LEAVE (" ++ show count ++ ")"]
	, Assignment leave0 (Load ddcSlotPtr)
	, Assignment leave1 (GetElemPtr True leave0 [LlvmIndexInt (-count)])
	, Store leave1 ddcSlotPtr

	, Assignment leave2 (Load ddcSlotBase)
	, Assignment leave3 (Compare LM_CMP_Ult leave1 leave2)
	, BranchIf leave3 (LMLocalVar lpanic LMLabel) (LMLocalVar lgood LMLabel)
	, MkLabel lpanic
	, Expr (Call StdCall (LMGlobalVar "_panicSlotUnderflow" (LMFunction _panicSlotUnderflow) External Nothing Nothing True) [] [NoReturn])
	, Branch (LMLocalVar lgood LMLabel)
	, MkLabel lgood

	, Comment ["---------------------------------------------------------------"]
	]
    where
	leave0	= LMNLocalVar "leave.0" ppObj
	leave1	= LMNLocalVar "leave.1" ppObj
	leave2	= LMNLocalVar "leave.2" ppObj
	leave3	= LMNLocalVar "leave.3" i1
	lpanic	= fakeUnique "leave.panic"
	lgood	= fakeUnique "leave.ok"


slotInit :: Unique -> Int -> [LlvmStatement]
slotInit _ count
 | count < 0
 = panic stage $ "Asked for " ++ show count ++ " GC slots."

slotInit _ count
 | count < 8
 =	Assignment init0 (Load ddcSlotPtr) : concatMap build [1 .. count]
    where
	init0 = LMNLocalVar "init.0" ppObj
	build n
	 =	let target = LMNLocalVar ("init.target." ++ show n) ppObj
		in	[ Assignment target (GetElemPtr False init0 [LlvmIndexInt (-n)])
			, Store nullObj target ]

slotInit initstart n
 | otherwise
 =	[ Assignment init0 (Load ddcSlotPtr)
	, Branch (LMLocalVar initloop LMLabel)

	, MkLabel initloop
	, Assignment index (Phi llvmWord [((LMLitVar (llvmWordLit 0)), (LMLocalVar initstart LMLabel)), (indexNext, (LMLocalVar initloop LMLabel))])
	, Assignment tmp (LlvmOp LM_MO_Add index (LMLitVar (llvmWordLit (-n))))

	, Assignment target (GetElemPtr False init0 [LlvmIndexVar tmp])
	, Store nullObj target

	, Assignment indexNext (LlvmOp LM_MO_Add index (LMLitVar (llvmWordLit 1)))
	, Assignment initdone (Compare LM_CMP_Eq indexNext (LMLitVar (llvmWordLit n)))
	, BranchIf initdone (LMLocalVar initend LMLabel) (LMLocalVar initloop LMLabel)
	, MkLabel initend
	]
    where
	initloop	= fakeUnique "init.loop"
	initend		= fakeUnique "init.end"
	init0		= LMNLocalVar "init.0" ppObj
	index		= LMNLocalVar "init.index" llvmWord
	indexNext	= LMNLocalVar "init.index.next" llvmWord
	initdone	= LMNLocalVar "init.done" i1
	tmp		= LMNLocalVar "init.tmp" llvmWord
	target		= LMNLocalVar "init.target" ppObj

--------------------------------------------------------------------------------
-- Helpers.

toLlvmType :: Type -> LlvmType
toLlvmType (TPtr t)	= LMPointer (toLlvmType t)
toLlvmType TObj		= structObj
toLlvmType TVoid	= LMVoid
toLlvmType t		= panic stage $ "toLlvmType " ++ show t ++ "\n"


toLlvmVar :: Var -> Type -> LlvmVar
toLlvmVar v t
 = case isGlobalVar v of
	True -> LMGlobalVar (seaVar False v) (toLlvmType t) External Nothing Nothing False
        False -> LMNLocalVar (seaVar True v) (toLlvmType t)


llvmWordLit :: Integral a => a -> LlvmLit
llvmWordLit n = LMIntLit (toInteger n) llvmWord


newUniqueLocal :: LlvmType -> IO LlvmVar
newUniqueLocal t
 = do	u <- newUnique
	return $ LMLocalVar u t


isGlobalVar :: Var -> Bool
isGlobalVar v 
 -- If the variable is explicitly set as global use the given name.
 | bool : _	<- [global | ISeaGlobal global <- varInfo v]
 = bool

 | otherwise
 = False


loadAddress :: LlvmVar -> LlvmExpression
loadAddress v = Load (pVarLift v)

--------------------------------------------------------------------------------
-- Data types and variables.

_panicOutOfSlots :: LlvmFunctionDecl
_panicOutOfSlots = LlvmFunctionDecl "_panicOutOfSlots" External CC_Ccc LMVoid FixedArgs [] ptrAlign

_panicSlotUnderflow :: LlvmFunctionDecl
_panicSlotUnderflow = LlvmFunctionDecl "_panicSlotUnderflow" External CC_Ccc LMVoid FixedArgs [] ptrAlign


ptrAlign :: Maybe Int
ptrAlign = Just Config.pointerBytes


ddcSlotPtr :: LlvmVar
ddcSlotPtr = pVarLift (LMGlobalVar "_ddcSlotPtr" ppObj External Nothing ptrAlign False)

ddcSlotMax :: LlvmVar
ddcSlotMax = pVarLift (LMGlobalVar "_ddcSlotMax" ppObj External Nothing ptrAlign False)

ddcSlotBase :: LlvmVar
ddcSlotBase = pVarLift (LMGlobalVar "_ddcSlotBase" ppObj External Nothing ptrAlign False)


ddcObj :: LlvmType
ddcObj = LMStruct [ i32 ]

structObj :: LlvmType
structObj = LMAlias ("struct.Obj", ddcObj)

pObj :: LlvmType
pObj = pLift structObj

ppObj :: LlvmType
ppObj = pLift pObj

nullObj :: LlvmVar
nullObj = LMLitVar (LMNullLit pObj)

