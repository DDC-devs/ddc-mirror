
-- This module can be type-checked  
--   with "ddc -load <file>" 
--   and  "ddc -check <file>"
-- but will not compile to object code yet as support for higher order
-- functions is not complete.
module Main 
export main 

import foreign abstract type
 Console  :: Effect

import foreign c value
 putStrLn :: [r : Region]. Ptr# r String# -> S Console Void#
 putStr   :: [r : Region]. Ptr# r String# -> S Console Void#
 showNat  :: [r : Region]. Nat#           -> Ptr# r String#

where

-- Define configurable syntax.
add x y = add# [Nat#] x y       -- used for (+)
sub x y = sub# [Nat#] x y       -- used for (-)
ge  x y = ge#  [Nat#] x y       -- used for (>=)


-------------------------------------------------------------------------------
-- | Cons lists.
data List (a : Data) where
        Nil     : List a
        Cons    : a -> List a -> List a


-- | Construct a list containing a single element.
singleton (x : a) : List a
 = Cons x Nil


-- | Construct a range of values.
enumFromTo (start : Nat#) (end : Nat#)
 = case start >= end of
        True#           -> singleton start
        False#          -> Cons start (enumFromTo (start + 1) end)


-- | Apply a worker function to all elements of a list, 
--   yielding a new list.
mapS (f : a -> S e b) (xx : List a) : S e (List b)
 = box case xx of
        Nil             -> Nil
        Cons x xs       -> Cons (run f x) (run mapS f xs)


-- | Apply a function to all elements of a list, 
--   yielding nothing.
eatS (f : a -> S e Unit) (xx : List a) : S e Unit
 = box case xx of
        Nil     -> ()

        Cons x xs       
         -> do  run f x
                run eatS f xs


-- | Reduce a list to a single value.
foldS (f : a -> b -> S e b) (z : b) (xx0 : List a) : S e b
 = letrec 
        go (acc : b) (xx : List a) : S e b
         = box case xx of
                Nil             -> acc
                Cons x xs       -> run go (run f x acc) xs
   in   go z xx0    


-- | Keep elements of a list that match the given predicate.
filterS (f : a -> S e Bool#) (xx : List a) : S e (List a)
 = box case xx of
        Nil             -> Nil
        Cons x xs       
         -> case run f x of
                True#   -> Cons x (run filterS f xs)
                False#  -> run filterS f xs


main (_ : Unit) : S Pure Unit
 = box do
        xx      = enumFromTo 0 10
        run eatS (\(x : Nat#). 
                putStrLn (showNat x)) xx
        ()

